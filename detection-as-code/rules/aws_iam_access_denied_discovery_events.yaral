rule aws_iam_access_denied_discovery_events {

  meta:
    author = "Google Cloud Security"
    description = "Detects excessive AccessDenied events within an hour timefram from an IAM User"
    mitre_attack_tactic = "Discovery"
    mitre_attack_technique = "Cloud Infrastructure Discovery"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1580/"
    mitre_attack_version = "v13.1"
    type = "Alert"
    data_source = "AWS CloudTrail"
    platform = "AWS"
    severity = "Low"
    priority = "Low"

  events:
    $cloudtrail.metadata.base_labels.namespaces = "LogStory"
    $cloudtrail.metadata.base_labels.namespaces = "LogStory"
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    $cloudtrail.security_result.rule_id = "AccessDenied"
    $cloudtrail.security_result.action = "BLOCK"
    $cloudtrail.principal.user.userid = $user_id
    $cloudtrail.target.application = $target_application
    $cloudtrail.metadata.product_event_type = $product_event_type

  match:
    $user_id over 1h

  outcome:
    $risk_score = max(35)
    $mitre_attack_tactic = "Discovery"
    $mitre_attack_technique = "Cloud Infrastructure Discovery"
    $mitre_attack_technique_id = "T1580"
    $event_count = count_distinct($cloudtrail.metadata.id)
    $network_http_user_agent = array_distinct($cloudtrail.network.http.user_agent)
    $principal_ip = array_distinct($cloudtrail.principal.ip)
    $principal_ip_country = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principal_user_display_name = array_distinct($cloudtrail.principal.user.user_display_name)
    $recipient_aws_account_id = array_distinct($cloudtrail.target.resource.attribute.labels["Recipient Account Id"])
    $aws_region = array_distinct($cloudtrail.principal.location.name)
    $target_resource_name = array_distinct($cloudtrail.target.resource.name)
    $target_resource_product_object_id = array_distinct($cloudtrail.target.resource.product_object_id)
    $security_result_description = array_distinct($cloudtrail.security_result.description)
    $target_applications = array_distinct($cloudtrail.target.application)
    $product_event_types = array_distinct($cloudtrail.metadata.product_event_type)

  condition:
    #cloudtrail > 5 and #target_application > 1 and #product_event_type > 1
}
