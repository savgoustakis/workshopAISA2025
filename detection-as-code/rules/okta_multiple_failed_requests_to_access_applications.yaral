rule okta_multiple_failed_requests_to_access_applications {

  meta:
    author = "Google Cloud Security"
    description = "Detects multiple failed requests to access applications"
    reference = "https://sec.okta.com/sessioncookietheft"
    mitre_attack_tactic = "Defense Evasion, Lateral Movement"
    mitre_attack_technique = "Use Alternate Authentication Material: Web Session Cookie"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1550/004/"
    mitre_attack_version = "v13.1"
    type = "Hunt"
    data_source = "Okta"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.base_labels.namespaces = "LogStory"
    $sso.metadata.base_labels.namespaces = "LogStory"
    $login.metadata.base_labels.namespaces = "LogStory"
    $sso.metadata.base_labels.namespaces = "LogStory"
    $login.metadata.product_name = "Okta"
    $login.metadata.vendor_name = "Okta"
    $login.metadata.event_type = "USER_UNCATEGORIZED"
    $login.metadata.product_event_type = "policy.evaluate_sign_on"
    $login.target.resource.resource_subtype = "AppInstance"
    $login.security_result.action = "CHALLENGE"
    $login.network.parent_session_id = $parentNetworkSessionId

    $sso.metadata.event_type = "USER_LOGIN"
    $sso.metadata.product_event_type = "user.authentication.sso"
    $sso.security_result.action = "ALLOW"
    $sso.target.resource.resource_subtype = "AppInstance"
    $sso.network.parent_session_id = $parentNetworkSessionId

  match:
    $parentNetworkSessionId over 15m

  outcome:
    $risk_score = max(
        // Baseline
        35 +
        // Based on Okta Behavior Detection pattern analysis
        if($login.security_result.description = /New Geo-Location=POSITIVE/, 10) +
        if($login.security_result.description = /New Device=POSITIVE/, 20) +
        if($login.security_result.description = /New IP=POSITIVE/, 5) +
        if($login.security_result.description = /New City=POSITIVE/, 5) +
        if($login.security_result.description = /New State=POSITIVE/, 10) +
        if($login.security_result.description = /New Country=POSITIVE/, 10) +
        if($login.security_result.description = /Velocity=POSITIVE/, 10) +
        // Unauthorized target geographies
        if($login.target.ip_geo_artifact.location.country_or_region = "Cuba", 20) +
        if($login.target.ip_geo_artifact.location.country_or_region = "Iran", 20) +
        if($login.target.ip_geo_artifact.location.country_or_region = "North Korea", 20) +
        if($login.target.ip_geo_artifact.location.country_or_region = "Russia", 20) +
        if($login.target.ip_geo_artifact.location.country_or_region = "Syria", 20)
    )
    $mitre_attack_tactic = "Defense Evasion, Lateral Movement"
    $mitre_attack_technique = "Use Alternate Authentication Material: Web Session Cookie"
    $principal_user_agent = array_distinct($login.network.http.user_agent)
    $principal_ip = array_distinct($login.principal.ip)
    $principal_ip_country = array_distinct($login.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($login.principal.ip_geo_artifact.location.state)
    $principal_ip_city = array_distinct($login.principal.location.city)
    $security_result_summary = array_distinct($login.security_result.summary)
    $principal_user_managers_email_addresses = array_distinct($login.principal.user.managers.email_addresses)
    $principal_user_userid = array_distinct($login.principal.user.userid)
    $app_names = array_distinct($login.target.resource.name)
    $dc_app_name = count_distinct($login.target.resource.name)
    $security_result_description = array_distinct($login.security_result.description)

  condition:
    $login and $sso and $dc_app_name > 5
}
