rule aws_ec2_user_data_modified {

  meta:
    author = "Google Cloud Security"
    description = "Detect modifications to user data script on an EC2 instance."
    mitre_attack_tactic = "Privilege Escalation"
    mitre_attack_technique = "Boot or Logon Initialization Scripts"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1037/"
    mitre_attack_version = "v13.1"
    type = "Alert"
    data_source = "AWS CloudTrail"
    platform = "AWS"
    severity = "High"
    priority = "High"

  events:
    $cloudtrail.metadata.base_labels.namespaces = "LogStory"
    $cloudtrail.metadata.base_labels.namespaces = "LogStory"
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    $cloudtrail.metadata.product_event_type = "ModifyInstanceAttribute"
    $cloudtrail.target.resource.attribute.labels["userData"] != ""
    $cloudtrail.security_result.action = "ALLOW"
    $cloudtrail.target.resource_ancestors.product_object_id = $instanceId

  match:
    $instanceId over 1h

  outcome:
    $risk_score = max(75)
    $mitre_attack_tactic = "Privilege Escalation"
    $mitre_attack_technique = "Boot or Logon Initialization Scripts"
    $mitre_attack_technique_id = "T1037"
    $event_count = count_distinct($cloudtrail.metadata.id)
    $network_http_user_agent = array_distinct($cloudtrail.network.http.user_agent)
    $principal_ip = array_distinct($cloudtrail.principal.ip)
    $principal_ip_country = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principal_user_display_name = array_distinct($cloudtrail.principal.user.user_display_name)
    $recipient_aws_account_id = array_distinct($cloudtrail.target.resource.attribute.labels["Recipient Account Id"])
    $aws_region = array_distinct($cloudtrail.principal.location.name)
    $target_resource_name = array_distinct($cloudtrail.target.resource.name)
    $target_resource_product_object_id = array_distinct($cloudtrail.target.resource.product_object_id)

  condition:
    $cloudtrail
}
