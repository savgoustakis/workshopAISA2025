rule process_launch_vt_enrichment {

  meta:
    author = "Google Cloud Security"
    description = "Identify process creations that are tagged exploit in VT"
    type = "alert"
    tags = "vt enrichment"
    data_source = "crowdstrike"
    severity = "High"
    priority = "High"

  events:
    $process.metadata.base_labels.namespaces = "LogStory"
    $process.metadata.base_labels.namespaces = "LogStory"
    ($process.metadata.event_type = "PROCESS_LAUNCH" or $process.metadata.event_type = "FILE_CREATION")
    $process.principal.hostname = $hostname
    // Enriched field from VirusTotal integration, can be commented out or modified if not using
    any $process.target.file.tags = "exploit" nocase

  match:
    $hostname over 5m

  outcome:
    $risk_score = max(85)
    $event_count = count_distinct($process.metadata.id)
    // added to populate alert graph with additional context
    // Commented out principal.hostname because it is already represented in graph as match variable. If match changes, can uncomment to add to results
    //$principal_hostname = array_distinct($process.principal.hostname)
    $principal_process_pid = array_distinct($process.principal.process.pid)
    $principal_process_command_line = array_distinct($process.principal.process.command_line)
    $principal_process_file_sha256 = array_distinct($process.principal.process.file.sha256)
    $principal_process_file_full_path = array_distinct($process.principal.process.file.full_path)
    $principal_process_product_specfic_process_id = array_distinct($process.principal.process.product_specific_process_id)
    $principal_process_parent_process_product_specfic_process_id = array_distinct($process.principal.process.parent_process.product_specific_process_id)
    $target_process_pid = array_distinct($process.target.process.pid)
    $target_process_command_line = array_distinct($process.target.process.command_line)
    $target_process_file_sha256 = array_distinct($process.target.process.file.sha256)
    $target_process_file_full_path = array_distinct($process.target.process.file.full_path)
    $target_process_product_specfic_process_id = array_distinct($process.target.process.product_specific_process_id)
    $principal_user_userid = array_distinct($process.principal.user.userid)
    $target_file_sha256 = array_distinct($process.target.file.sha256)
    $target_file_full_path = array_distinct($process.target.file.full_path)

  condition:
    $process
}
