rule suspicious_download_office {
  
  meta:
    author = "Google Cloud Security"
    description = "Office Application downloading an executable(.exe in URL) or a suspiciously large file(>100KB)"
    type = "alert"
    data_source = "tanium, zscalar, crowdstrike"
    reference = "https://attack.mitre.org/software/S0160/"
    severity = "Critical"
    priority = "Critical"

  events:
    $edr.metadata.base_labels.namespaces = "LogStory"
    $proxy.metadata.base_labels.namespaces = "LogStory"
    $edr.metadata.base_labels.namespaces = "LogStory"
    $proxy.metadata.base_labels.namespaces = "LogStory"
    $edr.metadata.event_type = "PROCESS_LAUNCH"
    $edr.target.process.file.full_path = /excel/ nocase
    $edr.principal.hostname = $hostname

    $proxy.metadata.event_type = "NETWORK_HTTP"
    ($proxy.target.url = /\.exe$/ nocase or $proxy.network.received_bytes > 102400)
    $proxy.principal.hostname = $hostname

    $proxy.metadata.event_timestamp.seconds > $edr.metadata.event_timestamp.seconds

  match:
    $hostname over 5m

  outcome:
    $risk_score = max(95)
    //added to populate alert graph with additional context
    // Commented out principal.hostname because it is already represented in graph as match variable. If match changes, can uncomment to add to results
    //$principal_hostname = array_distinct($edr.principal.hostname)
    $principal_process_pid = array_distinct($edr.principal.process.pid)
    $principal_process_command_line = array_distinct($edr.principal.process.command_line)
    $principal_process_file_sha256 = array_distinct($edr.principal.process.file.sha256)
    $principal_process_file_full_path = array_distinct($edr.principal.process.file.full_path)
    $principal_process_product_specfic_process_id = array_distinct($edr.principal.process.product_specific_process_id)
    $principal_process_parent_process_product_specfic_process_id = array_distinct($edr.principal.process.parent_process.product_specific_process_id)
    $target_process_pid = array_distinct($edr.target.process.pid)
    $target_process_command_line = array_distinct($edr.target.process.command_line)
    $target_process_file_sha256 = array_distinct($edr.target.process.file.sha256)
    $target_process_file_full_path = array_distinct($edr.target.process.file.full_path)
    $target_process_product_specfic_process_id = array_distinct($edr.target.process.product_specific_process_id)
    $principal_user_userid = array_distinct($edr.principal.user.userid)
    $target_hostname = array_distinct($proxy.target.hostname)
    $target_url = array_distinct($proxy.target.url)

  condition:
    $edr and $proxy
}
